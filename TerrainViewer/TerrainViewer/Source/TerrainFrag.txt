#version 400
struct DirectionalLight
{
	vec3 color;
	float ambientIntensity;
	vec3 dir;
	float diffuseIntensity;
};

// Out data
out vec4 out_Color;

// In data
in float defaultHeight;
smooth in vec3 vertNormal;
smooth in vec3 halfDir;
in DirectionalLight light;
in vec2 snowUV;
in vec2 rockUV;
in vec2 grassUV;

//enum RenderState
//	{
//		WIREFRAME_RENDER_STATE	= 0,
//		NORMALS_RENDER_STATE	= 1,
//		SPLAT_MAP_RENDER_STATE	= 2,
//		AMBIENT_RENDER_STATE	= 3,
//		DIFFUSE_RENDER_STATE	= 4,
//		SPEC_RENDER_STATE		= 5,
//		PHONG_RENDER_STATE		= 6,
//		RENDER_STATE_COUNT		= 7
//	};

uniform int renderState;
uniform sampler2D snow;
uniform sampler2D rock;
uniform sampler2D grass;


vec4 BlinnPhongColor( vec3 norm, vec3 halfV, bool _hasSnow, float _snowFactor )
{
	vec4 retColor = vec4(0,0,0,0);

	//-----------------------------------------------------------------------------------------
	// AMBIENT COLOR CALCULATION
	if( renderState == 6
		|| renderState == 3 )
	{
		vec4 ambientColor = vec4(light.color * light.ambientIntensity, 0.0f);
		retColor += ambientColor;
	}

	//-----------------------------------------------------------------------------------------
	// DIFFUSE COLOR CALCULATION
	if( renderState == 6
		|| renderState == 4 )
	{
		vec4 diffuseColor = vec4(0,0,0,0);
		float diffuseFactor = max(dot(norm, normalize(-light.dir)), 0.0);
		diffuseColor = vec4(light.color, 0) * light.diffuseIntensity * diffuseFactor;
		retColor += diffuseColor;
	}

	//-----------------------------------------------------------------------------------------
	// SPECULAR COLOR CALCULATION
	if( renderState == 6
		|| renderState == 5 )
	{
		if(_hasSnow)
		{
			float nDotH = max(dot(norm, halfV),0.0f);

			float specColor = pow(nDotH, 5.f);
			// First value is the hard-coded value for the material's shininess factor
			retColor += vec4(vec3(.8f,.8f,.8f) * _snowFactor * specColor, 0);
		}
	}
	return retColor;
}

vec4 SplatMap(float normY)
{
	// Thresholds
	float minThreshSnow = .45f;
	float threshFullSnow = .55f;
	float minThreshRock = .87f;
	float threshFullRock = .85f;

	// Texture colors
	vec4 rockColor = vec4(1,0,0,1);
	vec4 snowColor = vec4(0,0,1,1);
	vec4 grassColor = vec4(0,1,0,1);

	//-----------------------------------------------------------------------------------------
	// Texture blending
	// HACK .1f is a hardcoded hack. We should upload the max height in case we change it
	float heightPct = defaultHeight / .7f;
	vec4 retColor = vec4(0,0,0,0);

	float rockLerpVal = ( normY - minThreshRock ) / (threshFullRock - minThreshRock);
	rockLerpVal = max( 0.0f, min( rockLerpVal, 1.f ) );
	
	float snowLerpVal = (heightPct - minThreshSnow) / (threshFullSnow - minThreshSnow);
	snowLerpVal = max( 0.0f, min( snowLerpVal, 1.f ) );

	retColor = mix( grassColor, snowColor, snowLerpVal );
	retColor = mix( retColor, rockColor, rockLerpVal );
	retColor.a = 1;
	return retColor;
}

vec4 TextureColors(float normY, inout bool _hasSnow, inout float _snowFactor)
{
	// Thresholds
	float minThreshSnow = .45f;
	float threshFullSnow = .55f;
	float minThreshRock = .87f;
	float threshFullRock = .85f;

	// Texture colors
	vec4 rockColor = texture2D(rock, rockUV);
	vec4 snowColor = texture2D(snow, snowUV);
	vec4 grassColor = texture2D(grass, grassUV);

	//-----------------------------------------------------------------------------------------
	// Texture blending
	// HACK .1f is a hardcoded hack. We should upload the max height in case we change it
	float heightPct = defaultHeight / .7f;
	vec4 retColor = vec4(0,0,0,0);

	float rockLerpVal = ( normY - minThreshRock ) / (threshFullRock - minThreshRock);
	rockLerpVal = max( 0.0f, min( rockLerpVal, 1.f ) );
	
	float snowLerpVal = (heightPct - minThreshSnow) / (threshFullSnow - minThreshSnow);
	snowLerpVal = max( 0.0f, min( snowLerpVal, 1.f ) );

	retColor = mix( grassColor, snowColor, snowLerpVal );
	retColor = mix( retColor, rockColor, rockLerpVal );

	_hasSnow = snowLerpVal > rockLerpVal;
	_snowFactor = snowLerpVal - rockLerpVal;
	retColor.a = 1;
	return retColor;
}

bool hasSnow = false;
float snowFactor = 0.0f;
void main(void)
{
	// Normal and half vector (blinn-phong) need to be normalized after
	// being passed from vertShader because they are being interpolated
	vec3 newNorm = normalize(vertNormal);
	vec3 newHalfV = normalize(halfDir);
	
	if( renderState == 0)
	{
		// Drawing wireframe white
		out_Color = vec4(0,0,0,1);
	}
	else if(renderState == 1)
	{
		//Render normals of each frag via color
		out_Color = vec4((newNorm + vec3(1,1,1)) * .5, 1);
	}
	else
	{
		// Get texture colors then use BlinnPhong calculation to get realistic lighting
		if(renderState != 2)
		{
			out_Color = TextureColors(newNorm.y, hasSnow, snowFactor);
			out_Color *= BlinnPhongColor( newNorm, newHalfV, hasSnow, snowFactor );
		}
		else
		{
			// Get splat map
			out_Color = SplatMap( newNorm.y );	
		}
		out_Color.a = 1;
	}
}
